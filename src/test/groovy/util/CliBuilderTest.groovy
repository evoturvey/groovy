/*
 *  Copyright 2003-2007 the original author or authors.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in
 *  compliance with the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software distributed under the License is
 *  distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *  implied.  See the License for the specific language governing permissions and limitations under the
 *  License.
 */

package groovy.util

import org.apache.commons.cli.GnuParser
import org.apache.commons.cli.OptionBuilder
import org.apache.commons.cli.PosixParser
import org.apache.commons.cli.BasicParser

/**
 *  Test class for the CliBuilder -- but then that is obvious from the name :-)
 *
 *  <p>There appear to be issues when using the <code>PosixParser</code> &ndash; when an option with a
 *  parameter is passed using a long form and a single letter parameter of some sort has been declared (the
 *  problem does not occur if no single letter option has been declared) then the value "--" is returned
 *  instead of the option parameter value.  This problem does not happen using the
 *  <code>GnuParser</code>.</p>
 *
 *  <p>There appears to be an issue with <code>GnuParser</code> &ndash; if only a long option is defined
 *  then the usual Groovy syntax for accessing the option fails to work.  It is fine if a short option of
 *  some sort is defined.  This must be a <code>CliBuilder</code>/<code>OptionAccessor</code> problem.  This
 *  problem does not happen with the <code>PosixParser</code>.</p>
 *
 *  @author Dierk KÃ¶nig
 *  @author Russel Winder
 */

class CliBuilderTest extends GroovyTestCase {

  StringWriter writer

  void setUp ( ) { writer = new StringWriter ( ) }


  void runSample ( cli , optionList ) {
    cli.h ( longOpt : 'help', 'usage information' )
    cli.c ( argName : 'charset' , args :1 , longOpt : 'encoding' , 'character encoding' )
    cli.i ( argName : 'extension' , optionalArg : true, 'modify files in place, create backup if extension is given (e.g. \'.bak\')' )
    def stringified = cli.options.toString ( )
    assert stringified =~ /i=. option: i  :: modify files in place, create backup if extension is given/
    assert stringified =~ /c=. option: c encoding  :: character encoding/
    assert stringified =~ /h=. option: h help  :: usage information/
    assert stringified =~ /encoding=. option: c encoding  :: character encoding/
    assert stringified =~ /help=. option: h help  :: usage information/
    def options = cli.parse ( optionList )
    assert options.hasOption ( 'h' )
    assert options.hasOption ( 'help' )
    assert options.h
    assert options.help
    if ( options.h ) { cli.usage ( ) }
    assertEquals  '''usage: groovy [option]* filename
 -c,--encoding <charset>   character encoding
 -h,--help                 usage information
 -i                        modify files in place, create backup if
                           extension is given (e.g. '.bak')''' , writer.toString ( ).tokenize ( '\r\n' ).join ( '\n' )
    //  Should really also try with "if ( options.help ) { cli.usage ( ) }" but we need a new StringWriter for that.
    assert options.hasOption ( 'c' )
    assert options.hasOption ( 'encoding' )
    assert options.encoding
    assertEquals 'ASCII', options.getOptionValue ( 'c' )
    assertEquals 'ASCII', options.getOptionValue ( 'encoding' )
    assertEquals 'ASCII', options.c
    assertEquals 'ASCII', options.encoding
    assertEquals false, options.noSuchOptionGiven
    assertEquals false, options.x
  }
  
  void testSampleShort_GnuParser ( ) {
    runSample ( new CliBuilder ( usage : 'groovy [option]* filename' , writer : new PrintWriter ( writer ) , parser : new GnuParser ( ) ) , [ '-h' , '-c' , 'ASCII' ] )
  }

  void testSampleShort_PosixParser ( ) {
    runSample ( new CliBuilder ( usage : 'groovy [option]* filename' , writer : new PrintWriter ( writer ) , parser : new PosixParser ( ) ) , [ '-h' , '-c' , 'ASCII' ] )
  }
  
  void testSampleLong_GnuParser ( ) {
    runSample ( new CliBuilder ( usage : 'groovy [option]* filename' , writer : new PrintWriter ( writer ) , parser : new GnuParser ( ) ) , [ '--help' , '--encoding' , 'ASCII' ] )
  }

  /*
   *  Cannot run this test because of the "--" instead of "something" problem.  See testLongAndShortOpts_PosixParser below.
   */
  void XXX_testSampleLong_PosixParser ( ) {
    runSample ( new CliBuilder ( usage : 'groovy [option]* filename' , writer : new PrintWriter ( writer ) , parser : new PosixParser ( ) ) , [ '--help' , '--encoding' , 'ASCII' ] )
  }

  void testMultipleArgs ( ) {
    def cli = new CliBuilder ( )
    cli.a ( longOpt :'arg' , args : 2 , valueSeparator : ',' as char , 'arguments' )
    def options = cli.parse ( [ '-a' , '1,2' ] )
    assertEquals '1' , options.a
    assertEquals ( [ '1' , '2' ] , options.as )
    assertEquals '1' , options.arg
    assertEquals ( [ '1' , '2' ] , options.args )
  }

  void testArgs ( ) {
    def cli = new CliBuilder ( )
    cli.a ( [:] , '' )
    def options = cli.parse ( [ '-a' , '1' , '2' ] )
    assertEquals ( [ '1' , '2' ] , options.arguments ( ) )
  }

  void testFailedParsePrintsUsage ( ) {
    def cli = new CliBuilder ( writer : new PrintWriter ( writer ) )
    cli.x ( required : true , 'message' )
    def options = cli.parse ( [ ] )
    assertEquals '''error: Missing required option: x
usage: groovy
 -x   message''',  writer.toString ( ).tokenize ( '\r\n' ).join ( '\n' )
    }

  void testLongOptsOnly_GnuParser ( ) {
    //
    //  This test behaves differently when run individually using testOne compared to when run with testAll.  This is WORRYING.
    //
    def cli = new CliBuilder ( writer : new PrintWriter ( writer ) , parser : new GnuParser ( ) )
    def anOption = OptionBuilder.withLongOpt ( 'anOption' ).hasArg ( ).withDescription ( 'An option.' ).create ( )
    cli.options.addOption ( anOption )
    def options = cli.parse ( [ '-v' , '--anOption' , 'something' ] )
    cli.usage ( )
    assert ! options.v
    //
    //  When run individually using testOne, the <arg> is missing and this test fails.
    //
    assertEquals '''usage: groovy
    --anOption <arg>   An option.''' , writer.toString ( ).tokenize ( '\r\n' ).join ( '\n' )
    /*
     *  When run with testAll, anOption is not an accessible property.  This is WRONG.
     *
    assertEquals 'something' , options.getOptionValue ( 'anOption' )
    assertEquals 'something' , options.anOption
    */
    assertEquals null , options.getOptionValue ( 'anOption' )
    assertEquals false , options.anOption
  }
  void testLongOptsOnly_PosixParser ( ) {
    def cli = new CliBuilder ( writer : new PrintWriter ( writer ) , parser : new PosixParser ( ) )
    def anOption = OptionBuilder.withLongOpt ( 'anOption' ).hasArg ( ).withDescription ( 'An option.' ).create ( )
    cli.options.addOption ( anOption )
    def options = cli.parse ( [ '-v' , '--anOption' , 'something' ] )
    cli.usage ( )
    assertEquals '''usage: groovy
    --anOption <arg>   An option.''' , writer.toString ( ).tokenize ( '\r\n' ).join ( '\n' )
    assertEquals 'something' , options.getOptionValue ( 'anOption' )
    assertEquals 'something' , options.anOption
    assert ! options.v
  }

  void testLongAndShortOpts_BasicParser ( ) {
    def options = createOptionsWithLongAndShortOpts ( new BasicParser ( ) )
    assertEquals 'something' , options.getOptionValue ( 'anOption' )
    assertEquals 'something' , options.anOption
  }

  void testLongAndShortOpts_PosixParser ( ) {
    def options = createOptionsWithLongAndShortOpts ( new PosixParser ( ) )
    assertEquals '--' , options.getOptionValue ( 'anOption' )
    assertEquals '--' , options.anOption
  }

  void testLongAndShortOpts_GnuParser ( ) {
    def options = createOptionsWithLongAndShortOpts ( new GnuParser ( ) )
    assertEquals 'something' , options.getOptionValue ( 'anOption' )
    assertEquals 'something' , options.anOption
  }

  private createOptionsWithLongAndShortOpts ( parser ) {
    def cli = new CliBuilder ( writer : new PrintWriter ( writer ) , parser : parser )
    def anOption = OptionBuilder.withLongOpt ( 'anOption' ).hasArg ( ).withDescription ( 'An option.' ).create ( )
    cli.options.addOption ( anOption )
    cli.v ( longOpt : 'verbose' , 'verbose mode' )
    def options = cli.parse ( [ '-v' , '--anOption' , 'something' ] )
    cli.usage ( )
    assertEquals '''usage: groovy
    --anOption <arg>   An option.
 -v,--verbose          verbose mode''' , writer.toString ( ).tokenize ( '\r\n' ).join ( '\n' )
    assert options.v
    return options
  }

}
